apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
}

task deleteJar(type: Delete) {
    delete 'libs/MH_GenericRecyclerAdapter.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', 'MH_GenericRecyclerAdapter.jar')
}

createJar.dependsOn(deleteJar, build)

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


install {
    repositories.mavenInstaller {
        pom.project {
            name 'MHGenericAdapter'
            description 'a generic adapter for recycler view'
            url 'https://github.com/MH480/MHGenericAdapter'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'mh.sanwix.com'
            artifactId 'GenericAdapter'
            version '0.1.1'

            licenses {
                license {
                    name 'Apache-2.0'
                    url 'http://www.apache.org/licenses/'
                }
            }
            scm {
                connection 'https://github.com/MH480/MHGenericAdapter.git'
                url 'https://github.com/MH480/MHGenericAdapter'

            }
            developers {
                developer {
                    id = 'MH480'
                    name 'MH'
                }
            }
        }
    }
}


Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'MHGenericAdapter'
        name = 'mh480'
        userOrg = 'sanwix'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/MH480/MHGenericAdapter.git'
        publish = true
        version {
            name = '0.1.1'
            desc = 'a generic adapter for recycler view'
            released = new Date()
            vcsTag = 'v0.1.1'
        }
    }
}
